<?xml version="1.0" encoding="utf-8"?>
<s:GridItemEditor xmlns:fx="http://ns.adobe.com/mxml/2009" 
				  xmlns:s="library://ns.adobe.com/flex/spark" 
				  xmlns:mx="library://ns.adobe.com/flex/mx" width="100%" height="100%">
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import mx.collections.IList;
			import mx.controls.Alert;
			private static var _dataProvider:IList;
			public static function set dataProvider(value:IList):void{
				_dataProvider=value;
				if(instence){
					instence.dataProvider=value;
				}
			}
			private static var instence:ComboBoxEditor;
			override public function prepare():void{
				instence=this;
				combox.labelField=column.dataField;
				combox.labelFunction=column.labelFunction;
				if(_dataProvider){
					this.dataProvider=_dataProvider;
				}
				super.prepare();
				combox.openDropDown();
			}
			override public function discard():void{
				instence=null;
				_dataProvider=null;
				combox.dataProvider=null;
				super.discard();
			}
			override public function save():Boolean{
				//设置各字段，刷新列表
				if(!combox.dataProvider||combox.dataProvider.length==0||!combox.selectedItem){
					return false;
				}
				if(column.itemToLabel(data)==combox.itemToLabel(combox.selectedItem)){
					return false;
				}else{
					for(var pro:String in combox.selectedItem){
						data[pro]=combox.selectedItem[pro];
					}
					value=combox.itemToLabel(combox.selectedItem);
					data["isEdited"]=true;
					return true;
				}
			}
			override public function setFocus():void{
				combox.setFocus();
			}
			public function set dataProvider(value:IList):void{
				if(combox){
					combox.dataProvider=value
					combox.selectedItem=getSelectItem();
				}
			}
			private function getSelectItem():Object{
				//设置选中项
				for(var i:int=combox.dataProvider.length-1;i>-1;i--){
					if(combox.itemToLabel(combox.dataProvider.getItemAt(i))==column.itemToLabel(data)){
						return combox.dataProvider.getItemAt(i);
					}
				}
				return null;
			}
		]]>
	</fx:Script>
	<s:DropDownList width="100%" height="100%" id="combox" close="dataGrid.endItemEditorSession()" mouseDown="event.stopImmediatePropagation()"/>
</s:GridItemEditor>
